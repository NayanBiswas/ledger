# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("app") {
  testonly = true

  deps = [
    ":application_tests",
    ":integration_tests",
    ":ledger_app",
    ":unittests",
  ]
}

source_set("lib") {
  sources = [
    "branch_tracker.cc",
    "branch_tracker.h",
    "constants.cc",
    "constants.h",
    "fidl/bound_interface.h",
    "ledger_impl.cc",
    "ledger_impl.h",
    "ledger_manager.cc",
    "ledger_manager.h",
    "ledger_repository_factory_impl.cc",
    "ledger_repository_factory_impl.h",
    "ledger_repository_impl.cc",
    "ledger_repository_impl.h",
    "page_impl.cc",
    "page_impl.h",
    "page_manager.cc",
    "page_manager.h",
    "page_snapshot_impl.cc",
    "page_snapshot_impl.h",
    "page_utils.cc",
    "page_utils.h",
  ]

  deps = [
    "//apps/ledger/services",
    "//apps/ledger/src/callback",
    "//apps/ledger/src/cloud_sync/impl",
    "//apps/ledger/src/convert",
    "//apps/ledger/src/glue",
    "//apps/ledger/src/glue/data_pipe",
    "//apps/ledger/src/storage/impl:lib",
    "//apps/ledger/src/storage/public",
    "//lib/fidl/cpp/bindings",
    "//lib/ftl",
  ]
}

executable("ledger_app") {
  output_name = "ledger"
  sources = [
    "app.cc",
  ]

  deps = [
    ":lib",
    "//apps/ledger/services",
    "//apps/ledger/src/storage/impl:lib",
    "//apps/ledger/src/storage/public",
    "//apps/modular/lib/app",
    "//apps/network/services",
    "//lib/fidl/cpp/bindings",
    "//lib/ftl",
    "//lib/mtl",
  ]
}

executable("unittests") {
  output_name = "ledger_unittests"

  testonly = true

  sources = [
    "ledger_manager_unittest.cc",
    "page_impl_unittest.cc",
    "page_manager_unittest.cc",
  ]

  deps = [
    ":lib",
    "//apps/ledger/src/cloud_sync/impl",
    "//apps/ledger/src/convert",
    "//apps/ledger/src/glue",
    "//apps/ledger/src/storage/fake:lib",
    "//apps/ledger/src/storage/public",
    "//apps/ledger/src/storage/test",
    "//apps/ledger/src/test",
    "//lib/fidl/cpp/bindings",
    "//lib/ftl",
    "//lib/mtl",
    "//third_party/gtest",
  ]
}

executable("integration_tests") {
  output_name = "ledger_integration_tests"

  testonly = true

  sources = [
    "ledger_integration_tests.cc",
  ]

  deps = [
    ":lib",
    "//apps/ledger/services",
    "//apps/ledger/src/convert",
    "//apps/ledger/src/storage/fake:lib",
    "//apps/ledger/src/storage/public",
    "//apps/ledger/src/test",
    "//lib/fidl/cpp/bindings",
    "//lib/mtl",
    "//third_party/gtest",
  ]
}

executable("application_tests") {
  output_name = "ledger_apptests"

  testonly = true

  sources = [
    "ledger_apptest.cc",
  ]

  deps = [
    "//apps/ledger/services",
    "//apps/ledger/services:services_sync",
    "//apps/modular/lib/app",
    "//lib/fidl/cpp/bindings",
    "//lib/mtl",
    "//third_party/gtest",
  ]

  data_deps = [
    ":ledger_app",
  ]
}
